import groovy.json.JsonBuilder

buildscript {
    repositories {
        mavenLocal()
        mavenCentral()
        jcenter()
    }

    dependencies {
        classpath "com.google.guava:guava:17.0"
        classpath "com.tkruse.gradle:gradle-groovysh-plugin:1.0.4"
        classpath 'com.github.jengelman.gradle.plugins:shadow:1.1.1'
        classpath 'org.yaml:snakeyaml:1.14'
    }
}

apply plugin: 'java'
apply plugin: 'groovy'
apply plugin: 'maven'
apply plugin: 'application'
apply plugin: 'codenarc'
apply plugin: 'checkstyle'
apply plugin: 'jacoco'
apply plugin: 'com.github.johnrengelman.shadow'
apply plugin: 'maven-publish'
apply plugin: 'com.github.tkruse.groovysh'

def buildNumber() {
    def buildNum = '0'
    if (hasProperty('buildNumber')) {
        buildNum = buildNumber
    }
    buildNum
}

sourceCompatibility = 1.8
targetCompatibility = 1.8
version = "0.1.${buildNumber()}"

// Project details
group = "au.com.dius"
mainClassName = "au.com.dius.service.StencilService"
description = "Stencil Service"

def configuration = "conf/stencil-service.yml"

[compileJava, compileTestJava]*.options*.encoding = 'UTF-8'

repositories {
    mavenLocal()
    mavenCentral()
}

dependencies {
    compile 'org.codehaus.groovy:groovy-all:2.4.1:indy'
    compile "io.dropwizard:dropwizard-core:${dropwizardVersion}"
    compile "io.dropwizard:dropwizard-hibernate:${dropwizardVersion}"
    compile 'com.hubspot.dropwizard:dropwizard-guice:0.7.2'
    compile "com.fasterxml.jackson.core:jackson-core:${jacksonVersion}"
    compile "com.fasterxml.jackson.jaxrs:jackson-jaxrs-json-provider:${jacksonVersion}"
    compile "com.fasterxml.jackson.dataformat:jackson-dataformat-xml:${jacksonVersion}"
    compile "com.fasterxml.jackson.dataformat:jackson-dataformat-yaml:${jacksonVersion}"
    compile 'com.xeiam:sundial:1.1.3'
    compile 'com.squareup.retrofit:retrofit:1.9.0'
    compile 'org.apache.poi:poi-ooxml:3.11'
    compile 'org.ostermiller:utils:1.07.00'
    compile 'org.apache.poi:poi-ooxml-schemas:3.11'
    compile 'org.apache.poi:ooxml-schemas:1.1'
    compile 'net.sf.opencsv:opencsv:2.3'

    testCompile "io.dropwizard:dropwizard-testing:${dropwizardVersion}"
    testCompile "junit:junit:4.11"
    testCompile "org.mockito:mockito-core:1.9.5"
    testCompile "au.com.dius:pact-jvm-consumer-groovy_2.10:${pactJvmVersion}"
    testCompile 'org.hamcrest:hamcrest-all:1.3'
    testCompile 'au.com.ioof.asis.authenticator:jws-test-rule:1.1.1'
    testCompile 'com.cyrusinnovation:mockito-groovy-support:1.3'
}

tasks.withType(GroovyCompile) {
    groovyOptions.optimizationOptions.indy = true
}

checkstyleMain {
    classpath += configurations.compile
}

shadowJar {
    mergeServiceFiles()
}

task wrapper(type: Wrapper) {
    gradleVersion = '2.3'
}

test {
    testLogging.showStandardStreams = true
}